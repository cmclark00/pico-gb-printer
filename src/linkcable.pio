// Pin configuration matching existing hardware schematic
.define PUBLIC PIN_SCK     2    ; Clock - Game Boy Pin 3 → GPIO 2
.define PUBLIC PIN_SIN     0    ; Serial In (to Pico) - Game Boy Pin 4 → GPIO 0  
.define PUBLIC PIN_SOUT    3    ; Serial Out (from Pico) - Game Boy Pin 2 → GPIO 3

//// ALTERNATIVE CONFIGURATION FOR RP2040-Zero (if rewiring)
// .define PUBLIC PIN_SCK     16   ; Clock - Game Boy Pin 5
// .define PUBLIC PIN_SIN     15   ; Serial In - Game Boy Pin 3
// .define PUBLIC PIN_SOUT    14   ; Serial Out - Game Boy Pin 2

//// STACKSMASHING BOARD
// .define PUBLIC PIN_SCK     0
// .define PUBLIC PIN_SIN     1
// .define PUBLIC PIN_SOUT    2

; Program name
.program linkcable

.wrap_target
    set  x, 6
    wait 0 gpio PIN_SCK [0]     ; wait for falling edge
    pull noblock                ; pull value for transmission from pico
    out null, 24                ; shift left by 24
    out pins, 1                 ; out the MSB bit
    wait 1 gpio PIN_SCK [2]     ; wait for rising edge
bitloop:
        in pins, 1              ; input bit
        wait 0 gpio PIN_SCK [2] ; wait for falling edge
        out pins, 1             ; output rest of the bits one by one
        wait 1 gpio PIN_SCK [1] ; wait for rising edge
    jmp x-- bitloop             ; loop through the rest of the bits
    in pins, 1                  ; input rest of the bits one by one
    push noblock                ; push the received value to pico
    irq 0
.wrap

% c-sdk {

static inline void linkcable_program_init(PIO pio, uint sm, uint offset) {
    pio_sm_config c = linkcable_program_get_default_config(offset);

    pio_sm_set_consecutive_pindirs(pio, sm, PIN_SCK, 1, false);

    sm_config_set_in_pins(&c, PIN_SIN);
    pio_sm_set_consecutive_pindirs(pio, sm, PIN_SIN, 1, false);
    sm_config_set_in_shift(&c, false, false, LINKCABLE_BITS);

    sm_config_set_out_pins(&c, PIN_SOUT, 1);
    pio_sm_set_consecutive_pindirs(pio, sm, PIN_SOUT, 1, true);
    sm_config_set_out_shift(&c, false, false, LINKCABLE_BITS);

//    sm_config_set_clkdiv(&c, 5);                // Set clock division (Commented out, this one runs at full speed)

    pio_gpio_init(pio, PIN_SCK);
    pio_gpio_init(pio, PIN_SIN);
    pio_gpio_init(pio, PIN_SOUT);

    pio_sm_init(pio, sm, offset, &c);

//    pio_sm_set_enabled(pio, sm, true);          // Set the state machine running (commented out, I'll start this in the C)
}
%}